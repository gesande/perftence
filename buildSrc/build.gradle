import static org.gradle.logging.StyledTextOutput.Style

import org.gradle.logging.StyledTextOutputFactory
import org.gradle.plugins.ide.eclipse.model.AbstractLibrary
import org.gradle.plugins.ide.eclipse.model.Classpath
import org.gradle.plugins.ide.eclipse.model.ClasspathEntry
import org.gradle.plugins.ide.eclipse.model.internal.FileReferenceFactory


apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'java'

repositories {  flatDir { dirs 'lib' }  }

dependencies {
    compile gradleApi()
    groovy localGroovy()
    compile project.fileTree(dir: 'lib', include: '*.jar', exclude: '*-sources.jar')
}

version = '1.0'

eclipse.classpath.defaultOutputDir = file('target/classes')

ext.buildLibrary='my-gradle-build-1.0.3'

eclipse {
    classpath {
        file {
            whenMerged { Classpath cp ->
                String gradleHome = gradle.getGradleHomeDir().absolutePath.replace(File.separator, '/')
                String gradleSrc = "${gradleHome}/src"
                def entrySourcePath = new FileReferenceFactory().fromPath(gradleSrc)
                def outputFactory = services.get(StyledTextOutputFactory).create("gradlePlugins.eclipseClasspath")
                cp.entries.each { ClasspathEntry entry ->
                    if ((entry in AbstractLibrary) && (entry.library.file.name.startsWith('gradle-'))) {
                        entry.sourcePath = entrySourcePath
                        outputFactory.withStyle(Style.Info).println("Forked for patch for ${entry}")
                    }
                    if( ((entry in AbstractLibrary) && entry.library.file.name.startsWith(buildLibrary)) ) {
                        entry.sourcePath = new FileReferenceFactory().fromPath("${projectDir}/lib-sources/${buildLibrary}-sources.jar")
                        outputFactory.withStyle(Style.Info).println("Forked for patch for ${buildLibrary}")
                    }
                }
            }
        }
    }
}

